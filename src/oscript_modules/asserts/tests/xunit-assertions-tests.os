Перем хЮнит;


Функция ПолучитьСписокТестов(юТест) Экспорт
	ВсеТесты = Новый Массив;

	ВсеТесты.Добавить("ТестДолжен_Проверить_МетодСуществует_НеВыбрасываетИсключениеДляСуществующегоМетода");
	ВсеТесты.Добавить("ТестДолжен_Проверить_МетодСуществует_ВыбрасываетИсключениеДляОтсутствующегоМетода");
	ВсеТесты.Добавить("ТестДолжен_Проверить_МетодСуществует_ВыбрасываетИсключениеСУказаннымТекстом");
	
	Возврат ВсеТесты;
КонецФункции


Процедура ТестДолжен_Проверить_МетодСуществует_НеВыбрасываетИсключениеДляСуществующегоМетода() Экспорт
	Попытка
		хЮнит.ПроверитьМетодСуществует(Новый Массив, "Добавить");
	Исключение
		ВызватьИсключение ОписаниеОшибки() + ". Проверка существующего метода вызвала исключение, хотя не должна была";
	КонецПопытки;
КонецПроцедуры


Процедура ТестДолжен_Проверить_МетодСуществует_ВыбрасываетИсключениеДляОтсутствующегоМетода() Экспорт
	Попытка
		хЮнит.ПроверитьМетодСуществует(Новый Массив, "ТакогоИмениМетодаНеДолжноБыть");
	Исключение
		Возврат;
	КонецПопытки;

	// тест должен завершиться в обработке исключения
	// если дошло сюда - это ошибка
	ВызватьИсключение "Проверка существования у объекта несуществующего метода должна была выбросить исключение, но этого не произошло";
КонецПроцедуры


Процедура ТестДолжен_Проверить_МетодСуществует_ВыбрасываетИсключениеСУказаннымТекстом() Экспорт
	Перем ЧастьТекста;
	
	ЧастьТекста = "ЭтотТекстДолженБытьВТекстеИсключения*#1~";
	Попытка
		хЮнит.ПроверитьМетодСуществует(Новый Массив, "ТакогоИмениМетодаНеДолжноБыть", ЧастьТекста);
	Исключение
		Если 0 = СтрНайти(ОписаниеОшибки(), ЧастьТекста) Тогда
			ВызватьИсключение ОписаниеОшибки() + ". Текст исключения не содержит ожидаемого текста";
		Иначе
			Возврат;
		КонецЕсли;
	КонецПопытки;

	// тест должен завершиться в обработке исключения
	// если дошло сюда - это ошибка
	ВызватьИсключение "Проверка отсутствующего метода должна была выбросить исключение, но этого не произошло";
КонецПроцедуры


Процедура Инициализация()
	хЮнит = ЗагрузитьСценарий(ОбъединитьПути(ТекущийСценарий().Каталог, "..", "src", "xunit-asserts.os"));
КонецПроцедуры


Инициализация();
